use ['to_i'] in 'type'

@tokens = []
@pos = 0
@size = 0
@EOF = {'type' : 'EOF'}

def parse(ts):
	@tokens = ts
	@pos = 0
	@size = @tokens.size
	return expression()
end

def expression():
	return additive()
end

def additive():
	result = multiplicative()
	while(true):
		if match_t('PLUS') result += multiplicative() continue end
		if match_t('MINUS') result -= multiplicative() continue end
		break
	end
	return result
end

def multiplicative():
	result = unary()
	while(true):
		if match_t('STAR') result *= multiplicative() continue end
		if match_t('SLASH') result /= multiplicative() continue end
		break
	end
	return result
end

def unary():
	if match_t('MINUS') return -1 * primary() end
	return primary()
end

def primary():

	current = get(0)

	if match_t('NUMBER')
		return to_i(current.value)
	end

	if match_t('LPAREN')
		result = expression()
		match_t('RPAREN')
		return result
	end

	print 'Error :)'
	return false
end

def match_t(type):
	current = get(0)
	if type != current.type
		return false
	end
	@pos++
	return true
end

def get(rel_pos):
	position = @pos + rel_pos
	if position >= @size
		return @EOF
	end
	return @tokens[position]
end